//===================================
// Utility operations.
//===================================
// Creates a new ECore annotation
// with the specified name,
// adding an EString to String
// map entry with the stated name
// and value.
//===================================
operation createAnn(name, key, value) {
	var ann = new Target!EAnnotation;
	if(key <> null)
		ann.details.add(createDetail(key, value));
	ann.source = name;
	return ann;
}

//===================================
// Creates a new ECore annotation
// detail with the specified key
// and value, to be appended to a
// previously created annotation.
//===================================
operation createDetail(key, value) {
	var kvp = new Target!EStringToStringMapEntry;
	kvp.key = key;
	kvp.value = value;
	return kvp;
}

//===================================
// Creates a new ECore enum literal
// with the specified name and value.
//===================================
operation createLiteral(name, value) {
	var litVal = new Target!EEnumLiteral;
	litVal.name = name;
	litVal.value = value;
	return litVal;
}
//===================================
// Creates an ECore attribute with
// the specified name and type.
//===================================
operation createAttr(name, type) {
	var attr = new Target!EAttribute;
	attr.name = name;
	attr.eType = type;	
	return attr;
}

//===================================
// Creates an ECore reference with
// the specified parameters.
//===================================
operation createRef(name, cont, type, upperBound) {
	var ref = new Target!EReference;
	ref.name = name;
	ref.containment = cont;
	ref.eType = type;
	ref.upperBound = upperBound;
	return ref;
}

//===================================
// Add diagram contents to Circuit.
//===================================
operation resolveType(type) {
	var eString = ECore!EDataType.all.selectOne(dt|dt.name='EString');
	var eInt = ECore!EDataType.all.selectOne(dt|dt.name='EInt');
	var eFloat = ECore!EDataType.all.selectOne(dt|dt.name='EFloat');
	var eBoolean = ECore!EDataType.all.selectOne(dt|dt.name='EBoolean');
	switch(type) {
		case Source!PropertyType#STRING: return eString;
		case Source!PropertyType#INTEGER: return eInt;
		case Source!PropertyType#FLOAT: return eFloat;
		case Source!PropertyType#BOOLEAN: return eBoolean;
	}
}

//===================================
// The follwing pre blocks initialize
// the Circuit ECore model by
// creating a series of constructs,
// including key EuGENia annotations.
//===================================
// Creates the Circuit package.
//===================================
pre createPackage {
	var pkg = new Target!EPackage;
	pkg.name = 'circuit';
	pkg.nsPrefix = 'circuit';
	pkg.nsURI = 'http://www.intec.edu.do/abdiel/circuit';
}

//===================================
// Creates the diagram base Circuit
// class.
//===================================
pre createCircuitClass {
	var circuit = new Target!EClass;
	pkg.eClassifiers.add(circuit);
	circuit.name = 'Circuit';
	var gmfDiagram = createAnn('gmf.diagram', 'onefile', 'true');
	gmfDiagram.details.add(createDetail('rcp', 'true'));
	gmfDiagram.details.add(createDetail('diagram.extension', 'abl'));
	circuit.eAnnotations.add(gmfDiagram);
}

pre createJoint {
	var joint = new Target!EClass;
	pkg.eClassifiers.add(joint);
	joint.name = 'Joint';
	joint.abstract = true;
}

//===================================
// Creates the Polarity enumeration.
// TODO Could this be a rule instead?
//===================================
pre createPolarity {
	var polarity = new Target!EEnum;
	pkg.eClassifiers.add(polarity);
	polarity.name = 'Polarity';
	polarity.eLiterals.add(createLiteral('NEUTRAL', 1));
	polarity.eLiterals.add(createLiteral('POSITIVE', 2));
	polarity.eLiterals.add(createLiteral('NEGATIVE', 3));
}

//===================================
// Creates the instance Pin class.
//===================================
pre createPin { 
	var pin = new Target!EClass;
	pkg.eClassifiers.add(pin);
	pin.name = 'Pin';
	pin.eSuperTypes.add(joint);
	var gmfNode = createAnn('gmf.node', 'label', 'name');
	gmfNode.details.add(createDetail('label.icon', 'false'));
	pin.eAnnotations.add(gmfNode);
	var eString = ECore!EDataType.all.selectOne(dt|dt.name='EString');
	pin.eStructuralFeatures.add(createAttr('name', eString));
	pin.eStructuralFeatures.add(createAttr('polarity', polarity));
}

//===================================
// Creates the Port class.
//===================================
pre createPort {
	var port = new Target!EClass;
	pkg.eClassifiers.add(port);
	port.name = 'Port';
	var gmfNode = createAnn('gmf.node', 'label', 'name');
	gmfNode.details.add(createDetail('figure', 'ellipse'));
	gmfNode.details.add(createDetail('size', '20,20'));
	gmfNode.details.add(createDetail('label.placement', 'none'));
	gmfNode.details.add(createDetail('label.icon', 'false'));
	port.eAnnotations.add(gmfNode);
	var eString = ECore!EDataType.all.selectOne(dt|dt.name='EString');
	port.eStructuralFeatures.add(createAttr('name', eString));
	port.eStructuralFeatures.add(createAttr('protocol', eString));
}

//===================================
// Creates the PortWiring link class.
//===================================
pre createPortWiring {
	var portWiring = new Target!EClass;
	pkg.eClassifiers.add(portWiring);
	portWiring.name = 'PortWiring';
	var gmfLink = createAnn('gmf.link', 'source', 'port');
	gmfLink.details.add(createDetail('target', 'pin'));
	gmfLink.details.add(createDetail('style', 'dash'));
	portWiring.eAnnotations.add(gmfLink);	
	var eString = ECore!EDataType.all.selectOne(dt|dt.name='EString');
	portWiring.eStructuralFeatures.add(createAttr('role', eString));
	portWiring.eStructuralFeatures.add(createRef('pin', false, pin, 1));
	portWiring.eStructuralFeatures.add(createRef('port', false, port, 1));	
}

//===================================
// Creates the abstract Part class.
//===================================
pre createPart {
	var part = new Target!EClass;
	pkg.eClassifiers.add(part);
	part.name = 'Part';
	part.abstract = true;
	part.eStructuralFeatures.add(createAttr('name', eString));
	// Add pin container
	var pinCont = createRef('pins', true, pin, -1);
	part.eStructuralFeatures.add(pinCont);
	// Add GMF compartment annotation to pin container
	var compartment = createAnn('gmf.compartment', 'collapsible', 'false');
	compartment.details.add(createDetail('layout', 'list'));
	pinCont.eAnnotations.add(compartment);
	// Add ports container
	var portCont = createRef('ports', true, port, -1);
	part.eStructuralFeatures.add(portCont);
	// Add GMF affixed annotation to port container
	portCont.eAnnotations.add(createAnn('gmf.affixed', null, null));	
}

//===================================
// Creates the Wire link class.
//===================================
pre createWire {
	var wire = new Target!EClass;
	pkg.eClassifiers.add(wire);
	wire.name = 'Wire';
	var gmfLink = createAnn('gmf.link', 'source', 'source');
	gmfLink.details.add(createDetail('target', 'target'));
	gmfLink.details.add(createDetail('source.constraint', 'self <> oppositeEnd'));
	gmfLink.details.add(createDetail('target.constraint', 'self <> oppositeEnd'));
	wire.eAnnotations.add(gmfLink);
	//
	wire.eStructuralFeatures.add(createRef('source', false, joint, 1));
	wire.eStructuralFeatures.add(createRef('target', false, joint, 1));
}

//===================================
// Creates the Net node class.
//===================================
pre createNet {
	var net = new Target!EClass;
	pkg.eClassifiers.add(net);
	net.name = 'Net';
	net.eSuperTypes.add(joint);
	var eString = ECore!EDataType.all.selectOne(dt|dt.name='EString');
	net.eStructuralFeatures.add(createAttr('name', eString));
	net.eAnnotations.add(createAnn('gmf.node', 'label', 'name'));
}

//===================================
// Creates the NetWire link class.
//===================================
/*
pre createNetWire {
	var netWire = new Target!EClass;
	pkg.eClassifiers.add(netWire);
	netWire.name = 'NetWire';
	var gmfLink = createAnn('gmf.link', 'source', 'source');
	gmfLink.details.add(createDetail('target', 'target'));
	netWire.eAnnotations.add(gmfLink);
	//
	netWire.eStructuralFeatures.add(createRef('source', false, pin, 1));
	netWire.eStructuralFeatures.add(createRef('target', false, net, 1));
}
*/

//===================================
// Creates the PortConnection
// link class.
//===================================
pre createPortConnection {
	var portConn = new Target!EClass;
	pkg.eClassifiers.add(portConn);
	portConn.name = 'PortConnection';
	var gmfLink = createAnn('gmf.link', 'source', 'source');
	gmfLink.details.add(createDetail('target', 'target'));
	//gmfLink.details.add(createDetail(
		//'source.constraint', '(self <> oppositeEnd) and (self.protocol = oppositeEnd.protocol)'));
	portConn.eAnnotations.add(gmfLink);
	//
	portConn.eStructuralFeatures.add(createRef('source', false, port, 1));
	portConn.eStructuralFeatures.add(createRef('target', false, port, 1));
}

//===================================
// Add diagram contents to Circuit.
//===================================
post addDiagramContents {
	circuit.eStructuralFeatures.add(createRef('parts', true, part, -1));
	circuit.eStructuralFeatures.add(createRef('wires', true, wire, -1));
	circuit.eStructuralFeatures.add(createRef('nets', true, net, -1));
	// circuit.eStructuralFeatures.add(createRef('netWires', true, netWire, -1));
	circuit.eStructuralFeatures.add(createRef('portWires', true, portWiring, -1));
	circuit.eStructuralFeatures.add(createRef('portConns', true, portConn, -1));
}

//===================================
// PartSpec2EClass rule
// For each concrete PartSpecitication
// in the source ABDIEL model, create
// an ECore EClass in order to allow
// EuGENia to generate a tool and graph
// node for it.  The generated EClass
// extends Part and has attributes that
// mirror the transformed PartSpecification's
// associated Properties.
//===================================
rule PartSpec2EClass
transform partSpec : Source!PartSpecification
to eClass : Target!EClass {
	var ckt = Target!EPackage.all.selectOne(p|p.name='circuit');
	eClass.name := partSpec.name;
	ckt.eClassifiers.add(eClass);
	eClass.eSuperTypes.add(Target!EClass.all.selectOne(c|c.name='Part'));
	//
	var gmfNode = createAnn('gmf.node', 'label', 'name');
	gmfNode.details.add(createDetail('label.pattern', '{0}:' + eClass.name));
	eClass.eAnnotations.add(gmfNode);	
	//	
	for(prop:Source!Property in partSpec.properties) {
		var attr = new Target!EAttribute;
		eClass.eStructuralFeatures.add(attr);
		attr.name = prop.name;
		attr.eType = resolveType(prop.type);
	}
}

//===================================
// Informative post block to let us know
// the transformation has finished.
//===================================
post wrapUp {
 	'Transformation finished'.println();
}